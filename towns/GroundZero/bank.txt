//===== eAthena Script ========================================
//= Ground Zero Bank Area
//===== By: ==================================================
//= Au{R}oN (Translate by Alan)
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= eAthena 1.0+
//===== Description: =========================================
//= Ground Zero Bank
//===== Additional Comments: =================================
//= Converted from Aegis
//= 1.0 Add all Bank NPC (TEMPORARY NAME and FACING PROBABLY INCORRECT) [Au{R}oN]
//= 1.1 Add correct name, and change some dialogue. [Au{R}oN]
//============================================================

//=============================================
//==================Bankers====================
//=============================================


ea_in02,135,29,3	script	Bank Clerk::GZ	903,{
set .@name$,"[^0000ffBanker^000000]";
mes .@name$;
mes "What would you like to do?";
next;
menu "- Use the bank",banking,"- Nothing",-;
close;

banking:
set @cost,500;
mes .@name$;
mes "Welcome to the First Bank of Ground Zero. How can I help you today?";
next;
menu "- I'd like to make a deposit.",Ldeposit,"- I'd like to make a withdrawl.",Lwithdrawl,"- What's my current balance?",Lbalance,"Cancel",Lcancel;

Ldeposit:
	mes .@name$;
	mes "Very well... How much would you like to deposit? The maximum you can deposit at once is 999,999 Zeny.";
	next;
	if (@cost > 0) goto Ldepocost;
	goto Ldepocont;

	Ldepocost:
		mes .@name$;
		mes "Oh and don't forget there is a " + @cost + " Zeny charge on all transactions.";
		next;
		goto Ldepocont;

Ldepocont:
	input @deposit;
	if (@deposit < 1) goto Lrealamount;
	if (@deposit > Zeny) goto Lneedzeny;
	if (@deposit > (Zeny - @cost)) goto Lneedzeny2;
	set Zeny,Zeny - @deposit;
	set Zeny,Zeny - @cost;
	set #bankstorage,#bankstorage + @deposit;
	mes .@name$;
	mes "Thank you very much... Your zeny is in good hands.";
	close;

Lwithdrawl:
	mes .@name$;
	mes "Very well... How much would you like to withdraw? The maximum you can withdraw at one time is 999,999 Zeny";
	next;
	if (@cost > 0) goto Lwithcost;
	goto Lwithcont;

	Lwithcost:
		mes .@name$;
		mes "Oh and don't forget there is a " + @cost + " Zeny charge on all transactions.";
		next;
		goto Lwithcont;

Lwithcont:
	input @withdrawl;
	if (@withdrawl < 1) goto Lrealamount;
	if (@withdrawl > #bankstorage) goto Lneedzeny3;
	if ((@cost > Zeny) && ((Zeny + @withdrawl) > @cost)) goto Lcostask; 
	if (@cost > Zeny) goto Lneedzeny2;
	goto Lwithcont2;

	Lcostask:
		mes .@name$;
		mes "You don't have the Zeny for the transaction fee right now. Would you like me to take the fee directly from your withdrawl?";
		next;
		menu "Yes please.",Lwithtake,"No thank you.",Lcancel;

	Lwithtake:
		mes .@name$;
		mes "Ok then.";
		set @withdrawl,@withdrawl - @cost;
		set #bankstorage,#bankstorage - @cost;
		set @cost,0;
		next;
		goto Lwithcont2;

Lwithcont2:
	set Zeny,Zeny - @cost;
	set Zeny,Zeny + @withdrawl;
	set #bankstorage,#bankstorage - @withdrawl;
	mes .@name$;
	mes "There's your Zeny. Have a good day.";
	close;

Lbalance:
	mes .@name$;
	mes "Hmmmm lemme check the paper work.";
	next;
	mes "*Rustle, Rustle*";
	next;
	mes .@name$;
	mes "You currently have " + #bankstorage + " Zeny in your account.";
	close;

Lrealamount:
	mes .@name$;
	mes "Don't play jokes with me please. Next time ask for a real amount.";
	close;

Lneedzeny:
	mes .@name$;
	mes "You don't have enough Zeny to make that deposit.";
	close;

Lneedzeny2:
	mes .@name$;
	mes "You don't have enough Zeny to cover the transaction fee.";
	close;

Lneedzeny3:
	mes .@name$;
	mes "You don't have enough Zeny in your account.";
	close;

Lcancel:
	mes .@name$;
	mes "Very well... come again soon.";
	close;}

